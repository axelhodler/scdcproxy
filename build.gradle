buildscript {
	ext {
		springBootVersion = '2.0.1.RELEASE'
	}
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
		classpath 'org.springframework.cloud:spring-cloud-contract-gradle-plugin:1.2.4.RELEASE'
	}
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

group = 'com.hodler'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8

repositories {
	mavenCentral()
	maven { url "https://repo.spring.io/snapshot" }
	maven { url "https://repo.spring.io/milestone" }
}


ext {
	springCloudVersion = 'Finchley.BUILD-SNAPSHOT'
}

dependencies {
	compile('org.springframework.cloud:spring-cloud-starter-openfeign')
	testCompile "javax.ws.rs:javax.ws.rs-api:2.1"
	testCompile "org.springframework.boot:spring-boot-starter-jersey"
	testCompile('org.springframework.boot:spring-boot-starter-test')
	testCompile('org.springframework.cloud:spring-cloud-starter-contract-stub-runner')
	testCompile('org.springframework.cloud:spring-cloud-starter-contract-verifier')
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
		mavenBom "org.springframework.cloud:spring-cloud-contract-dependencies:1.2.4.RELEASE"
	}
}

apply plugin: 'spring-cloud-contract'

contracts {
	testMode = 'JaxRsClient'
	baseClassForTests = 'com.hodler.scdcproxy.GoogleMapsBase'
	basePackageForTests = 'com.hodler.scdcproxy'
}

repositories {
	mavenLocal()
}

apply plugin: 'java'
apply plugin: 'maven-publish'

task sourceJar(type: Jar, dependsOn: classes) {
	classifier 'sources'
	from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier = 'javadoc'
	from javadoc.destinationDir
}

publishing {
	publications {
		// workaround to avoid double upload and overwrites in artifact repository
		// spring-cloud-contract gradle plugin overrides pom.xml on publish
		// https://github.com/spring-cloud/spring-cloud-contract/issues/482
		stubs(MavenPublication) {
			from components.java

			artifact verifierStubsJar
			artifact sourceJar
			artifact javadocJar
		}
	}
}

test {
	testLogging {
		events "PASSED", "FAILED", "SKIPPED"
		exceptionFormat "full"
	}
}
